buildscript {
    ext {
        springBootVersion = '2.0.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
    }
}

plugins {
    id 'org.unbroken-dome.test-sets' version '2.1.1'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

group = 'com.github.manedev79'
version = '1.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

testSets {
    integrationTest
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.4'
    implementation 'org.springframework.boot:spring-boot-starter-validation:'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    implementation 'org.projectlombok:lombok:1.18.4'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2:1.4.197'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.assertj:assertj-core:3.6.2'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.50.5'
    integrationTestImplementation sourceSets.main.output
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name 'manedev79/timesheet'
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}
