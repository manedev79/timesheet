buildscript {
    ext {
        springBootVersion = '2.2.0.RELEASE'
        lombokVersion = '1.18.10'
        swaggerVersion = '2.9.2'
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'org.springframework.boot' version "$springBootVersion"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id 'com.palantir.docker' version '0.22.1'
    id 'org.unbroken-dome.test-sets' version '2.2.1'
}

group = 'com.github.manedev79'
version = '1.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

testSets {
    integrationTest
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-validation:'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation "io.springfox:springfox-swagger2:${swaggerVersion}"
    implementation "io.springfox:springfox-swagger-ui:${swaggerVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation "org.springframework.boot:spring-boot-starter-data-mongodb"
    runtimeOnly 'org.springframework.boot:spring-boot-devtools'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.assertj:assertj-core:3.6.2'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.50.5'
    integrationTestImplementation sourceSets.main.output
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/dependency")
}

docker {
    name 'manedev79/timesheet'
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}
